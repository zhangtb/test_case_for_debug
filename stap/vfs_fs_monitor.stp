#!/bin/env stap

global read_stat;
global read_byte;
global write_stat;
global write_byte;

probe vfs.read.return
{
	if (devname!="N/A") {
		read_stat[execname(), pid(), devname, ino]++;
		read_byte[execname(), pid(), devname, ino] += $return;
	}
}

probe vfs.write.return
{
	if (devname!="N/A") {
		write_stat[execname(), pid(), devname, ino]++;
		write_byte[execname(), pid(), devname, ino] += $return;
	}
}


probe timer.s($1) 
{
	
	printf("%-25s, fs read:\n", ctime(gettimeofday_s()));
	foreach ([n, p, d, i] in read_stat- limit 10) {
		printf("%-20s%d %5s %10u %10d %10d\n", n, p, d, i, 
			read_stat[n, p, d, i],
			read_byte[n, p, d, i]);
		
	}
	printf("%-25s, fs write:\n", ctime(gettimeofday_s()));
	delete read_stat;
	delete read_byte;
	foreach ([n, p, d, i] in write_stat- limit 10) {
		printf("%-20s%d %5s %10u %10d %10d\n", n, p, d, i, 
			write_stat[n, p, d, i],
			write_byte[n, p, d, i]);
		
	}
	delete write_stat;
	delete write_byte;
	printf("\n\n");
}

